apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "org.sonarqube"

android {
    compileSdkVersion app.compileSdk
    buildToolsVersion app.buildTools

    defaultConfig {
        applicationId "com.kaloglu.duels"

        minSdkVersion app.minSdk
        targetSdkVersion app.targetSdk

        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility java.sourceCompatibility
        targetCompatibility java.targetCompatibility
    }
    flavorDimensions 'default'
    productFlavors {
        prod {
            dimension 'default'
            buildConfigField 'String', 'NETWORK_PROTOCOL', networkProtocol
            buildConfigField 'String', 'END_POINT', endpoint
            buildConfigField 'String', 'LOCAL_DB', localdb
        }

    }

    sourceSets{
        main{
            res.srcDirs = getSourceDirectories()
        }
    }

}

dependencies {
    addDependencies(dependencies, appDependencies)
}

static def getSourceDirectories() {
    def directories = new ArrayList<>()
    directories.add('src/main/res')
    def layoutsDir = new File('App/src/main/res/layout')

    layoutsDir.eachDir {
        directories.add(it.canonicalPath)
        def fragmentsDir = new File(it.canonicalPath + '/fragments')
        if (fragmentsDir.exists()) {
            fragmentsDir.eachDir {
                directories.add(it.canonicalPath)
            }
        }
    }

    return directories
}

apply plugin: 'com.google.gms.google-services'
